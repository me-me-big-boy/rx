set dotenv-load := false

# Show all available recipes
@_default:
  echo "\nAPI\n==="
  just --list --unsorted

##########
# Python #
##########

# Print the Python version required by the project
@py-version:
  grep 'requires-python' pyproject.toml \
  | awk -F'"' '{print $2}' \
  | sed 's/^==//g; s/\.\*$//g'

# Install Python dependencies
install *args:
  pdm install {{ args }}

# Run PDM scripts
run *args:
  pdm run {{ args }}

##########
# Docker #
##########

# Execute the given command in the API service
exec *args:
  just ../dc exec api {{ args }}

# Show logs from the API service
logs:
  just ../dc logs -f api

##########
# Django #
##########

# Run Django management commands inside the container
dj *args:
  just exec python manage.py {{ args }}

# Create an interactive IPython shell with Django context
shell:
  just dj shell

# Create superuser with given username and password
create-su username="admin" email="admin@adm.in" password="admin":
  just exec env DJANGO_SUPERUSER_PASSWORD="{{ password }}" \
    python manage.py createsuperuser \
      --no-input \
      --username {{ username }} \
      --email "{{ email }}"

##########
# Pytest #
##########

# Run tests with Pytest
test run_slow="false" *args="":
  just exec {{ if run_slow == "true" { "env RUN_SLOW=true" } else { "" } }} \
    pytest {{ args }}
